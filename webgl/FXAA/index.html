<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>FXAA</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />
<link rel="stylesheet" type="text/css" media="screen,print" href="../../lib/webglutil.css" />
<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<script id="shader-fs" type="x-shader/x-fragment">
precision highp float;

uniform vec4 color;

void main() {
	gl_FragColor = color;
}
</script>
<script id="shader-vs" type="x-shader/x-vertex">
attribute vec3 position;

uniform mat4 mvMatrix;
uniform mat4 pMatrix;

void main() {
	gl_Position = pMatrix * mvMatrix * vec4(position, 1);
}
</script>
<script id="fxaa-vs" type="x-shader/x-vertex">
	attribute vec2 position;
	varying vec2 uv;

	void main() {
		uv = position.xy * 0.5 + 0.5;
		gl_Position = vec4(position, 0.0, 1.0);
	}
</script>
<script id="noaa-fs" type="x-shader/x-fragment">
	precision highp float;
	varying vec2 uv;
	uniform sampler2D tex;
	
	void main() {
		gl_FragColor = texture2D(tex, uv);
	}
</script>
<script id="fxaa-fs" type="x-shader/x-fragment">
	precision highp float;
	varying vec2 uv;
	uniform sampler2D tex;
	uniform vec2 offset;

	// 輝度を求める
	float FxaaLuma(vec4 rgb) {
//		return rgb.g * (0.587 / 0.299) + rgb.r;
		return rgb.g;
	}
	
	vec4 FxaaTexOff(sampler2D tex, vec2 pos, vec2 off) {
		return texture2D(tex, pos + off * offset);
	}
	
	vec4 FxaaPixelShader(vec2 pos, sampler2D tex,
						 vec2 fxaaQualityRcpFrame,
						 float fxaaQualitySubpix) {
		vec2 posM = pos;
		vec4 rgbyM = texture2D(tex, posM);
		float lumaM = FxaaLuma(rgbyM);
		float lumaS = FxaaLuma(FxaaTexOff(tex, posM, vec2( 0.0,  1.0)));
		float lumaE = FxaaLuma(FxaaTexOff(tex, posM, vec2( 1.0,  0.0)));
		float lumaN = FxaaLuma(FxaaTexOff(tex, posM, vec2( 0.0, -1.0)));
		float lumaW = FxaaLuma(FxaaTexOff(tex, posM, vec2(-1.0,  0.0)));
		float rangeMax = max(lumaM, max(max(lumaN, lumaW), max(lumaS, lumaE)));
		float rangeMin = min(lumaM, min(min(lumaN, lumaW), min(lumaS, lumaE)));
		float rangeMaxScaled = rangeMax * 0.166;
		float range = rangeMax - rangeMin;
		float rangeMaxClamped = max(0.0833, rangeMaxScaled);
		bool earlyExit = range < rangeMaxClamped;
		
		if(earlyExit)
			return rgbyM;
		
		float lumaNW = FxaaLuma(FxaaTexOff(tex, posM, vec2(-1.0, -1.0)));
		float lumaSE = FxaaLuma(FxaaTexOff(tex, posM, vec2( 1.0,  1.0)));
		float lumaNE = FxaaLuma(FxaaTexOff(tex, posM, vec2( 1.0, -1.0)));
		float lumaSW = FxaaLuma(FxaaTexOff(tex, posM, vec2(-1.0,  1.0)));
		
		float lumaNS = lumaN + lumaS;
		float lumaWE = lumaW + lumaE;
		float subpixRcpRange = 1.0 / range;
		float subpixNSWE = lumaNS + lumaWE;
		float edgeHorz1 = (-2.0 * lumaM) + lumaNS;
		float edgeVert1 = (-2.0 * lumaM) + lumaWE;
		
		float lumaNESE = lumaNE + lumaSE;
		float lumaNWNE = lumaNW + lumaNE;
		float edgeHorz2 = (-2.0 * lumaE) + lumaNESE;
		float edgeVert2 = (-2.0 * lumaN) + lumaNWNE;
		
		float lumaNWSW = lumaNW + lumaSW;
		float lumaSWSE = lumaSW + lumaSE;
		float edgeHorz4 = (abs(edgeHorz1) * 2.0) + abs(edgeHorz2);
		float edgeVert4 = (abs(edgeVert1) * 2.0) + abs(edgeVert2);
		float edgeHorz3 = (-2.0 * lumaW) + lumaNWSW;
		float edgeVert3 = (-2.0 * lumaS) + lumaSWSE;
		float edgeHorz = abs(edgeHorz3) + edgeHorz4;
		float edgeVert = abs(edgeVert3) + edgeVert4;
		
		float subpixNWSWNESE = lumaNWSW + lumaNESE;
		float lengthSign = fxaaQualityRcpFrame.x;
		bool horzSpan = edgeHorz > edgeVert;
		float subpixA = subpixNSWE * 2.0 + subpixNWSWNESE;
		
		if(!horzSpan) lumaN = lumaW;
		if(!horzSpan) lumaS = lumaE;
		if(horzSpan) lengthSign = fxaaQualityRcpFrame.y;
		float subpixB = (subpixA * (1.0 / 12.0)) - lumaM;
		
		float gradientN = lumaN - lumaM;
		float gradientS = lumaS - lumaM;
		float lumaNN = lumaN + lumaM;
		float lumaSS = lumaS + lumaM;
		bool pairN = abs(gradientN) >= abs(gradientS);
		float gradient = max(abs(gradientN), abs(gradientS));
		if(pairN) lengthSign = -lengthSign;
		float subpixC = clamp(abs(subpixB) * subpixRcpRange, 0.0, 1.0);
		
		vec2 posB = posM;
		vec2 offNP;
		offNP.x = !horzSpan ? 0.0 : fxaaQualityRcpFrame.x;
		offNP.y = horzSpan ? 0.0 : fxaaQualityRcpFrame.y;
		if(!horzSpan) posB.x += lengthSign * 0.5;
		if(horzSpan) posB.y += lengthSign * 0.5;
		
		vec2 posN = posB - offNP;
		vec2 posP = posB + offNP;
		float subpixD = (-2.0 * subpixC) + 3.0;
		float lumaEndN = FxaaLuma(texture2D(tex, posN));
		float subpixE = subpixC * subpixC;
		float lumaEndP = FxaaLuma(texture2D(tex, posP));
		
		if(!pairN) lumaNN = lumaSS;
		float gradientScaled = gradient * 1.0 / 4.0;
		float lumaMM = lumaM - lumaNN * 0.5;
		float subpixF = subpixD * subpixE;
		bool lumaMLTZero = lumaMM < 0.0;
		
		lumaEndN -= lumaNN * 0.5;
		lumaEndP -= lumaNN * 0.5;
		bool doneN = abs(lumaEndN) >= gradientScaled;
		bool doneP = abs(lumaEndP) >= gradientScaled;
		if(!doneN) posN -= offNP * 1.5;
		bool doneNP = (!doneN) || (!doneP);
		if(!doneP) posP += offNP * 1.5;
		
		if(doneNP) {
			if(!doneN) lumaEndN = FxaaLuma(texture2D(tex, posN));
			if(!doneP) lumaEndP = FxaaLuma(texture2D(tex, posP));
			if(!doneN) lumaEndN = lumaEndN - lumaNN * 0.5;
			if(!doneP) lumaEndP = lumaEndP - lumaNN * 0.5;
			doneN = abs(lumaEndN) >= gradientScaled;
			doneP = abs(lumaEndP) >= gradientScaled;
			if(!doneN) posN -= offNP * 2.0;
			doneN = (!doneN) || (!doneP);
			if(!doneP) posP += offNP * 2.0;
			
			if(doneNP) {
				if(!doneN) lumaEndN = FxaaLuma(texture2D(tex, posN));
				if(!doneP) lumaEndP = FxaaLuma(texture2D(tex, posP));
				if(!doneN) lumaEndN = lumaEndN - lumaNN * 0.5;
				if(!doneP) lumaEndP = lumaEndP - lumaNN * 0.5;
				doneN = abs(lumaEndN) >= gradientScaled;
				doneP = abs(lumaEndP) >= gradientScaled;
				if(!doneN) posN -= offNP * 4.0;
				doneN = (!doneN) || (!doneP);
				if(!doneP) posP += offNP * 4.0;
				
				if(doneNP) {
					if(!doneN) lumaEndN = FxaaLuma(texture2D(tex, posN));
					if(!doneP) lumaEndP = FxaaLuma(texture2D(tex, posP));
					if(!doneN) lumaEndN = lumaEndN - lumaNN * 0.5;
					if(!doneP) lumaEndP = lumaEndP - lumaNN * 0.5;
					doneN = abs(lumaEndN) >= gradientScaled;
					doneP = abs(lumaEndP) >= gradientScaled;
					if(!doneN) posN -= offNP * 12.0;
					doneN = (!doneN) || (!doneP);
					if(!doneP) posP += offNP * 12.0;
				}
			}
		}
		
		float dstN = posM.x - posN.x;
		float dstP = posP.x - posM.x;
		if(!horzSpan) dstN = posM.y - posN.y;
		if(!horzSpan) dstP = posP.y - posM.y;
		
		bool goodSpanN = (lumaEndN < 0.0) != lumaMLTZero;
		float spanLength = dstP + dstN;
		bool goodSpanP = (lumaEndP < 0.0) != lumaMLTZero;
		float spanLengthRcp = 1.0 / spanLength;
		
		bool directionN = dstN < dstP;
		float dst = min(dstN, dstP);
		bool goodSpan = directionN ? goodSpanN : goodSpanP;
		float subpixG = subpixF * subpixF;
		float pixelOffset = (dst * (-spanLengthRcp)) + 0.5;
		float subpixH = subpixG * fxaaQualitySubpix;
		
		float pixelOffsetGood = goodSpan ? pixelOffset : 0.0;
		float pixelOffsetSubpix = max(pixelOffsetGood, subpixH);
		if(!horzSpan) posM.x += pixelOffsetSubpix * lengthSign;
		if( horzSpan) posM.y += pixelOffsetSubpix * lengthSign;
		
		return vec4(texture2D(tex, posM));
	}
	
	void main() {
		gl_FragColor = FxaaPixelShader(uv, tex, offset, 0.75);
	}
</script>
<canvas id="canvas" width="256" height="256"></canvas>
<script type="text/javascript" src="../../lib/webglutil.js"></script>
<script type="text/javascript" src="index.js"></script>
</body>
</html>
